@echo off
cd /d "%~dp0"

echo ========================================
echo Building SteaMidra GUI Executable
echo ========================================
echo.

echo Cleaning old GUI build files...
if exist "build\build_sff_gui" rmdir /s /q "build\build_sff_gui"

if exist "third_party\SteamAuto Code\SteamAuto\SteamAutoCrack.CLI\SteamAutoCrack.CLI.csproj" (
    where dotnet >nul 2>&1
    if not errorlevel 1 (
        echo.
        echo Building SteamAutoCrack CLI...
        dotnet build "third_party\SteamAuto Code\SteamAuto\SteamAutoCrack.CLI\SteamAutoCrack.CLI.csproj" -c Release
        if exist "third_party\SteamAuto Code\SteamAuto\SteamAutoCrack.CLI\bin\Release\net9.0-windows\SteamAutoCrack.CLI.dll" (
            if not exist "third_party\SteamAutoCrack\cli" mkdir "third_party\SteamAutoCrack\cli"
            copy /y "third_party\SteamAuto Code\SteamAuto\SteamAutoCrack.CLI\bin\Release\net9.0-windows\SteamAutoCrack.CLI.*" "third_party\SteamAutoCrack\cli" >nul
            if exist "third_party\SteamAuto Code\SteamAuto\SteamAutoCrack.Core\bin\Release\net9.0-windows\SteamAutoCrack.Core.dll" (
                copy /y "third_party\SteamAuto Code\SteamAuto\SteamAutoCrack.Core\bin\Release\net9.0-windows\SteamAutoCrack.Core.*" "third_party\SteamAutoCrack\cli" >nul
            )
        )
        echo.
    )
)

echo.
echo Building GUI executable...
echo This may take 5-10 minutes...
echo.

REM Suppress pkg_resources deprecation from PyInstaller/build deps so log stays clean
set PYTHONWARNINGS=ignore::UserWarning
python -m PyInstaller build_sff_gui.spec

if errorlevel 1 (
    echo.
    echo ========================================
    echo BUILD FAILED!
    echo ========================================
    echo Install full requirements first:
    echo   pip install -r requirements.txt
    echo Or if grpcio-tools fails:
    echo   pip install -r requirements-consumer.txt
    pause
    exit /b 1
)

echo.
echo ========================================
echo BUILD SUCCESSFUL!
echo ========================================
echo.
echo Executable: dist\SteaMidra_GUI.exe
echo.

if exist "dist\SteaMidra_GUI.exe" (
    python -c "import os; size = os.path.getsize('dist/SteaMidra_GUI.exe'); print(f'Size: {size / (1024*1024):.1f} MB')"
    echo.
    echo Refreshing icon for SteaMidra_GUI.exe...
    move /y "dist\SteaMidra_GUI.exe" "dist\SteaMidra_GUI_temp.exe" >nul
    move /y "dist\SteaMidra_GUI_temp.exe" "dist\SteaMidra_GUI.exe" >nul
)

echo.
echo You can now run: dist\SteaMidra_GUI.exe
echo Settings will be saved in: dist\settings.bin
echo.
pause
